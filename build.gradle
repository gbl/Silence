buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.3+1.17'
    }
    repositories {
        maven {
            name = 'CrowdinTranslate source'
            url = "https://minecraft.guntram.de/maven/"
        }
    }
}

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id "com.modrinth.minotaur" version "1.1.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'silence'

repositories {
	maven {
		url = "https://maven.fabricmc.net/"
	}
        maven {
                url = "https://minecraft.guntram.de/maven/"
        }
	maven {
		url = "https://maven.terraformersmc.com/releases/"
	}
}

sourceCompatibility = 16
targetCompatibility = 16

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-21w39a.properties").newReader())

archivesBaseName = "silence"
ext.projectVersion = "1.0.0"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    mixin.defaultRefmapName = "silence-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    
    // This sucks, but the spigot server part needs Craft... classes which
    // aren't in spigots maven, so if you want to compile the mod, you need
    // to get a spigot.jar first. Sorry.

    compileOnly files('/home/gbl/SoftwareProjects/MyProjects/MinecraftPlugins/libraries/spigot-1.17.jar')

    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "com.terraformersmc:modmenu:${Versions['modmenu_version']}"
    modImplementation "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    include "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    modImplementation "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    include    "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

build {
    dependsOn downloadTranslations
}

jar {
    from "LICENSE"
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    token = System.getenv("MODRINTH_TOKEN")
    projectId = '1PmPJ4ss'
    versionNumber = project.version
    versionName = project.archivesBaseName
    releaseType = 'release'
    uploadFile = remapJar // This is the java jar task
    addGameVersion("${Versions['minecraft_version']}")
    addLoader('fabric')
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: "0"
    project {
        id = '530516'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java "+targetCompatibility)
        addGameVersion("Fabric")
        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}
